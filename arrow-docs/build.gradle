apply plugin: 'ank-gradle-plugin'
apply plugin: 'org.ajoberstar.git-publish'

dependencies {
    compile project(':ank-core')
    compile project(':arrow-syntax')
    compile project(':arrow-effects')
    compile project(':arrow-effects-rx2')
    compile project(':arrow-effects-kotlinx-coroutines')
    compile project(':arrow-optics')
    compile project(':arrow-recursion')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    compile "org.jetbrains.kotlinx:kotlinx-collections-immutable:$kotlinxCollectionsImmutableVersion"
    compile "io.kotlintest:kotlintest:$kotlinTestVersion"

    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3'
    compile "io.reactivex.rxjava2:rxjava:2.1.4"
}

task printcp << {
    println sourceSets.main.runtimeClasspath.each { println it }
}

ank {
    source = file("${projectDir}/docs")
    target = file("${projectDir}/build/site")
    classpath = sourceSets.main.runtimeClasspath
    includeAnkCore = false
}

gitPublish {
    repoUri = 'git@github.com:arrow/arrow.git'
    branch = 'gh-pages'

    contents {
        from 'build/site'
    }

    commitMessage = 'update site'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

compileKotlin.kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]
