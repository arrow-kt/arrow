plugins {
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version "0.10.1"
  id 'com.jfrog.bintray' version "1.8.4"
}
apply plugin: 'kotlin'

project.group = 'io.arrow-kt'
project.version = '0.0.1'

dependencies {
  compile gradleApi()
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
  compileOnly project(":compiler-plugin") { "embeddable" }
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

jar {
  // Embed compiler plugin in jar
  from(zipTree(configurations.named("compileClasspath").get().find { it.name.startsWith("compiler-plugin") }))

  manifest {
    attributes["Specification-Title"] = project.name
    attributes["Specification-Version"] = project.version
    attributes["Implementation-Title"] = "arrow.meta.plugin.gradle"
    attributes["Implementation-Version"] = project.version
  }
}

gradlePlugin {
  plugins {
    arrow {
      id = "arrow.meta.compiler.plugin"
      implementationClass = "arrow.meta.plugin.gradle.ArrowGradlePlugin"
    }
  }
}

pluginBundle {
  website = "http://arrow-kt.io/"
  vcsUrl = "https://github.com/47deg/arrow-meta-prototype.git"
  description = "The Arrow compiler plugin."
  tags = ["kotlin", "compiler", "arrow", "plugin", "meta"]

  plugins {
    arrow {
      id = "arrow.meta.compiler.plugin"
      displayName = "Arrow compiler plugin"
    }
  }

  mavenCoordinates {
    groupId = "arrow.meta.compiler.plugin"
  }
}