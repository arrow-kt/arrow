buildscript {
    ext {
        def customProperties = new Properties()
        customProperties.load(file("../arrow-libs/gradle.properties").newInputStream())
        customProperties.each { key, value -> set(key, value) }
        apply from: "../arrow-libs/gradle/setup.gradle"
    }
    dependencies {
        classpath "io.arrow-kt:arrow-ank-gradle:$VERSION_NAME"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$KOTLIN_VERSION"
    id "org.jetbrains.kotlin.kapt" version "$KOTLIN_VERSION"
    id "org.jlleitschuh.gradle.ktlint" version "10.1.0"
}

apply from: "../arrow-libs/gradle/main.gradle"

dependencies {
    implementation "io.arrow-kt:arrow-optics:$VERSION_NAME"
    implementation "io.arrow-kt:arrow-core:$VERSION_NAME"
    implementation "io.arrow-kt:arrow-fx-coroutines:$VERSION_NAME"
    implementation "io.arrow-kt:arrow-fx-stm:$VERSION_NAME"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$KOTLIN_VERSION"

    kapt "io.arrow-kt:arrow-meta:$VERSION_NAME"
}

// Ank Plugin is not applied for every library to avoid adding runtime dependencies
apply plugin: 'ank-gradle-plugin'

ank {
    source = file("${rootDir}/docs")
    target = file("${rootDir}/build/site")
    classpath = sourceSets.main.runtimeClasspath
}

task generateAndValidateDoc(type: Exec) {
  workingDir "../arrow-libs"
  commandLine "sh", "gradlew", "buildDoc"
}

task generateSite(type: Exec) {
  commandLine "sh", "gradle/scripts/generate-site.sh"
}

task buildSite {
  group = "documentation"
  description = "Generates and validates the documentation, and generates the website"
  dependsOn 'generateAndValidateDoc'
  dependsOn 'generateSite'
}

generateSite.mustRunAfter generateAndValidateDoc

clean.doFirst {
    delete "${projectDir}/docs/apidocs"
}
