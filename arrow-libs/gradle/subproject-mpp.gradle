/*
 * Copyright (C) 2020 The Arrow Authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

kotlin {
    explicitApi()
    jvm {
        // JVM target ignores Java sources and compiles only Kotlin source files.
        // Fix:
        withJava()

        compilations.all {
            kotlinOptions {
                freeCompilerArgs += ["-Xskip-runtime-version-check"]
                jvmTarget = "1.8"
            }
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
//    js(BOTH) {
    js(IR) {
        browser()
        nodejs()
    }
    linuxX64()

    mingwX64()

    macosX64()
    tvos()

    watchosArm32()
    watchosArm64()
    watchosX86()
    watchosX64()

    iosX64()
    iosArm64()
    iosArm32()
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$KOTLIN_VERSION"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
            }
        }
        jvmTest {
            dependencies {
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$KOTLINX_COROUTINES_VERSION"
                api "io.kotest:kotest-property:$KOTEST_VERSION"
                api "io.kotest:kotest-framework-engine:$KOTEST_VERSION"
                runtimeOnly "io.kotest:kotest-runner-junit5:$KOTEST_VERSION"
                api "io.kotest:kotest-assertions-core:$KOTEST_VERSION"
            }
        }
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$KOTLIN_VERSION"
            }
        }
        jsTest {
            dependencies {
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$KOTLINX_COROUTINES_VERSION"
                api "io.kotest:kotest-property:$KOTEST_VERSION"
                api "io.kotest:kotest-framework-engine:$KOTEST_VERSION"
                api "io.kotest:kotest-assertions-core:$KOTEST_VERSION"
            }
        }
        nativeMain {
            dependsOn(commonMain)
            macosX64Main.dependsOn(it)
            mingwX64Main.dependsOn(it)
            linuxX64Main.dependsOn(it)
            iosX64Main.dependsOn(it)
            iosArm64Main.dependsOn(it)
            iosArm32Main.dependsOn(it)
            watchosX86Main.dependsOn(it)
            watchosArm32Main.dependsOn(it)
            watchosArm64Main.dependsOn(it)
            watchosX64Main.dependsOn(it)
            tvosMain.dependsOn(it)
        }
        nativeTest {
            dependencies {
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$KOTLINX_COROUTINES_VERSION"
                api "io.kotest:kotest-property:$KOTEST_VERSION"
                api "io.kotest:kotest-framework-engine:$KOTEST_VERSION"
                api "io.kotest:kotest-assertions-core:$KOTEST_VERSION"
            }
            macosX64Test.dependsOn(it)
            mingwX64Test.dependsOn(it)
            linuxX64Test.dependsOn(it)
            iosX64Test.dependsOn(it)
            iosArm64Test.dependsOn(it)
            iosArm32Test.dependsOn(it)
            watchosX86Test.dependsOn(it)
            watchosArm32Test.dependsOn(it)
            watchosArm64Test.dependsOn(it)
            watchosX64Test.dependsOn(it)
            tvosTest.dependsOn(it)
        }
    }
}

ktlint {
    version = KTLINT_VERSION
}

clean.doFirst {
    delete "${projectDir}/docs/apidocs"
}

group = GROUP
archivesBaseName = POM_ARTIFACT_ID

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors()
}

tasks.jvmTest {
    useJUnitPlatform()
    testLogging {
        exceptionFormat 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
