buildscript {
    ext {
        apply from: "gradle/$COMMON_SETUP"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:$ATOMICFU_VERSION"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "$KTLINT_GRADLE_VERSION" apply false
    id "org.jetbrains.dokka" version "$DOKKA_VERSION" apply false
    id "ru.vyarus.animalsniffer" version "$ANIMALS_SNIFFER_VERSION" apply false
    id("io.kotest.multiplatform") version "$KOTEST_GRADLE_VERSION" apply false
    id "org.jetbrains.kotlin.multiplatform" version "$KOTLIN_VERSION" apply false
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "$BINARYCOMP_VERSION"
}

apply from: "gradle/$ROOT_PROJECT"

task generateDoc(type: Exec) {
  commandLine "sh", "gradlew", "dokkaGfm"
}

task runValidation(type: Exec) {
  workingDir "../arrow-site"
  commandLine "sh", "gradlew", "runAnk"
}

task buildDoc {
  group = "documentation"
  description = "Generates and validates the documentation"
  dependsOn 'generateDoc'
  dependsOn 'runValidation'
}

runValidation.mustRunAfter generateDoc

apiValidation {
    ignoredProjects += ["jekyll"]
}
