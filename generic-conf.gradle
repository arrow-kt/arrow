/*
 * Copyright (C) 2020 The Arrow Authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

allprojects {
    repositories {
        jcenter()
        maven { url 'https://kotlin.bintray.com/kotlinx' }
        maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local/" }
    }
}

subprojects { project ->

    clean.doFirst {
        delete "${projectDir}/docs/apidocs"
    }

    group = GROUP
    version = VERSION_NAME

    ktlint {
        version = KTLINT_VERSION
    }

    archivesBaseName = POM_ARTIFACT_ID

    compileKotlin.kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    sourceCompatibility = JAVA_VERSION
    targetCompatibility = JAVA_VERSION
}

configurations {
    buildableProjects
}

dependencies {
    buildableProjects(subprojects)
}

wrapper {
    gradleVersion = "6.0.1"
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    ARROW_GROUP = "Arrow"
    ARROW_GIT_GROUP = "Arrow (Git)"
}

task buildArrowDoc(type:Exec) {
    group = ARROW_GROUP
    description = "Generate API doc and run validation"
    commandLine "${rootDir}/../arrow/scripts/repository-build-doc.sh", "${rootProject.name}"
    mustRunAfter 'checkoutOrchestrator'
    dependsOn 'checkoutOrchestrator'
}

task checkoutOrchestrator(type:Exec) {
    commandLine './scripts/checkout-orchestrator.sh', "${rootDir}"
}

task installAllWithLocalDeps(type:Exec) {
    group = ARROW_GROUP
    description = "Install all the artifacts using local Arrow dependencies (no tests execution)"
    commandLine "${rootDir}/../arrow/scripts/libs-install.sh", "${rootProject.name}"
    mustRunAfter 'checkoutOrchestrator'
    dependsOn 'checkoutOrchestrator'
}

task testArrowProject(type:Exec) {
    mustRunAfter 'installAllWithLocalDeps'
    commandLine "${rootDir}/../arrow/scripts/repository-test.sh", "${rootProject.name}"
}

task testArrowProjects(type:Exec) {
    mustRunAfter 'installAllWithLocalDeps'
    commandLine "${rootDir}/../arrow/scripts/repositories-test.sh"
}

task buildWithLocalDeps {
    group = ARROW_GROUP
    description = "Build with local Arrow dependencies (tests execution included)"
    dependsOn 'installAllWithLocalDeps'
    dependsOn 'testArrowProject'
}

task buildAllWithLocalDeps {
    group = ARROW_GROUP
    description = "Build libs and examples with local Arrow dependencies (tests execution included)"
    dependsOn 'installAllWithLocalDeps'
    dependsOn 'testArrowProjects'
}

task gitPullAll(type:Exec) {
    group = ARROW_GIT_GROUP
    description = "Run git-pull for all the repositories"
    commandLine "${rootDir}/../arrow/scripts/repositories-git-pull.sh"
}

task gitPullOthers(type:Exec) {
    group = ARROW_GIT_GROUP
    description = "Run git-pull for the rest of the repositories"
    commandLine "${rootDir}/../arrow/scripts/repositories-others-git-pull.sh", "${rootProject.name}"
}
